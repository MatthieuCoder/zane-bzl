load("@aspect_rules_js//js:defs.bzl", "js_library", "js_run_devserver", "js_test")
load("@aspect_rules_ts//ts:defs.bzl", "ts_config")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//frontend:react-scripts/package_json.bzl", cra_bin = "bin")
load("@npm//frontend:tsconfig-to-swcconfig/package_json.bzl", tsconfig_to_swcconfig = "bin")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:mappings.bzl", "pkg_files")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")

npm_link_all_packages()

CRA_DEPS = [
    "//frontend/src",
    "//frontend/public",
    ":package_json",
    ":tailwind.config.js",
    ":node_modules/eslint-config-react-app",
    ":node_modules/react-dom",
    ":node_modules/react-scripts",
    ":node_modules/react",
    ":node_modules/typescript",
    ":node_modules/web-vitals",
    ":node_modules/tailwindcss",
]

cra_bin.react_scripts(
    # Note: If you want to change the name make sure you update BUILD_PATH below accordingly
    # https://create-react-app.dev/docs/advanced-configuration/
    name = "build",
    srcs = CRA_DEPS,
    args = ["build"],
    chdir = package_name(),
    env = {"BUILD_PATH": "./build"},
    out_dirs = ["build"],
)

js_test(
    name = "build_smoke_test",
    timeout = "short",
    data = ["build"],
    entry_point = "build_smoke_test.js",
)

js_run_devserver(
    name = "start",
    args = ["start"],
    command = "node_modules/.bin/react-scripts",
    data = CRA_DEPS + ["//frontend:package.json", ":node_modules/.bin/react-scripts"],
    chdir = package_name(),
)

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = ["//visibility:public"],
)

tsconfig_to_swcconfig.t2s(
    name = "write_swcrc",
    srcs = ["tsconfig.json"],
    args = [
        "--filename",
        "$(location tsconfig.json)",
    ],
    stdout = ".swcrc",
    visibility = ["//frontend:__subpackages__"],
)

js_library(
    name = "package_json",
    srcs = ["package.json"],
    visibility = ["//visibility:public"],
)

js_library(
    name = "jest_config",
    srcs = ["jest.config.js"],
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "build_files",
    strip_prefix = "build/",
    srcs = [":build"]
)

pkg_tar(
    name = "frontend_tar",
    srcs = [":build_files"],
    package_dir = "/usr/share/nginx/html"
)

oci_image(
    name = "frontend_image",
    base = "@nginx_debian_slim",
    tars = [
      ":frontend_tar",
    ],
    # Intentionally omit cmd/entrypoint to default to the base nginx container's cmd/entrypoint.
    # entrypoint = [],
    # cmd = [],
)

oci_tarball(
    name = "frontend_tarball",
    image = ":frontend_image",
    repo_tags = ["ourfrontend:latest"],
)